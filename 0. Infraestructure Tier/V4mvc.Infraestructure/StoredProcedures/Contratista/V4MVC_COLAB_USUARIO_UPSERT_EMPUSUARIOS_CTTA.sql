CREATE PROCEDURE [dbo].[V4MVC_COLAB_USUARIO_UPSERT_EMPUSUARIOS_CTTA]
(
  @RUT				NVARCHAR(10)
 ,@NOMBRES			NVARCHAR(50)
 ,@APELLIDOS		NVARCHAR(50)
 ,@UCREADOR			NVARCHAR(10)
 ,@IDEMPRESA		NVARCHAR(10)
 ,@TIPOUSUARIO		NVARCHAR(10)
 ,@CLAVE			NVARCHAR(50)
 ,@ACTIVO			NVARCHAR(2)
 ,@ENVIO_ALERTAS	NVARCHAR(2)
 ,@EMAIL			NVARCHAR(100)
)
AS
BEGIN
	DECLARE @CURR_FECHA NVARCHAR(8) = convert(NVARCHAR(8), getdate(), 112) /* yyyymmdd */
	DECLARE @CURR_TIEMPOFULL NVARCHAR(8) = convert(NVARCHAR(8), getdate(), 108) /* hh:mm:ss */
	IF(NOT EXISTS(
		(
			SELECT *
			FROM WORKERS
			WHERE RUT = @RUT
		)
	))
		BEGIN
			INSERT INTO WORKERS (RUT, NOMBRES, APELLIDOS, AUTOR, FECHAMOD, HORAMOD)
			VALUES (@RUT, @NOMBRES, @APELLIDOS, 'NO', @CURR_FECHA, @CURR_TIEMPOFULL)
		END

	IF(NOT EXISTS(
		SELECT *
		FROM COLAB_USUARIO
		WHERE
			(Rut = @RUT) AND
			(IdEmpresa = @IDEMPRESA)
	))
		BEGIN
			INSERT INTO COLAB_USUARIO
				(
					IdEmpresa, Rut, Clave, Correo_Electronico, TipoUsuario,
					Activo, EnvioAlertas, Fecha_Creacion, Fecha_Modificacion)
			VALUES
				(
					@IDEMPRESA, @RUT, @CLAVE, @EMAIL, @TIPOUSUARIO,
					@ACTIVO, @ENVIO_ALERTAS, getdate(), getdate())

			IF(NOT EXISTS(
				SELECT
					ACCION
				FROM COLAB_USUARIOSHIST
				WHERE
					EMPRESA = @IDEMPRESA AND
					USUARIO = @RUT AND
					FECHA = @CURR_FECHA AND
					HORA = @CURR_TIEMPOFULL
			))
				BEGIN
					INSERT INTO COLAB_USUARIOSHIST
					(
						EMPRESA, USUARIO, FECHA, HORA, QUIEN, ACCION, PERFIL)
					VALUES
					(
						@IDEMPRESA, @RUT, @CURR_FECHA, @CURR_TIEMPOFULL,
						@UCREADOR, 'INS', @TIPOUSUARIO)
				END
		END
	ELSE
		BEGIN
			UPDATE COLAB_USUARIO
			SET
				Clave = @CLAVE,
				Correo_Electronico = @EMAIL,
				TipoUsuario = @TIPOUSUARIO,
				Activo = @ACTIVO,
				EnvioAlertas = @ENVIO_ALERTAS,
				Fecha_Modificacion = getdate()
			WHERE
				(Rut = @RUT) AND
				(IdEmpresa = @IDEMPRESA)

			IF(NOT EXISTS(
				SELECT
					ACCION
				FROM COLAB_USUARIOSHIST
				WHERE
					EMPRESA = @IDEMPRESA AND
					USUARIO = @RUT AND
					FECHA = @CURR_FECHA AND
					HORA = @CURR_TIEMPOFULL
			))
				BEGIN
					INSERT INTO COLAB_USUARIOSHIST
						(
							EMPRESA, USUARIO, FECHA, HORA, QUIEN, ACCION, PERFIL)
					VALUES	
						(
							@IDEMPRESA, @RUT, @CURR_FECHA, @CURR_TIEMPOFULL,
							@UCREADOR, 'UPD', @TIPOUSUARIO)
				END
		END

END
